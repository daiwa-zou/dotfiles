# Set the path to the dotfiles directory
export ZSH=$HOME/.dotfiles

# Source environment variables from ~/.localrc if it exists
if [[ -f ~/.localrc ]]; then
  source ~/.localrc
fi

# Collect all zsh configuration files from the dotfiles directory
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# Load path configuration files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# Load all other zsh configuration files except path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# Initialize autocomplete
autoload -U compinit
compinit

# Load completion files after autocomplete is initialized
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

# Clear the config_files array
unset config_files

# Better history searching
# Credits: https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up arrow key
bindkey "^[[B" down-line-or-beginning-search # Down arrow key

